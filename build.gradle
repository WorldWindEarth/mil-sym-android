buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:2.2.3"
        classpath "org.ajoberstar:grgit:1.7.0"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.4.13"
    }
}

ext {
    bintrayUser = System.getenv("BINTRAY_USER") ?: project.hasProperty('bintrayUser') ? bintrayUser : ''
    bintrayKey  = System.getenv('BINTRAY_KEY') ?: project.hasProperty('bintrayKey') ? bintrayKey : ''

    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7

    git = org.ajoberstar.grgit.Grgit.open(file('.')) // Open the Git repository in the current directory.
    revision = git.head().id                         // Get commitId of HEAD.
}

subprojects  {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply from: "${rootDir}/gradle/bintray-support.gradle"

    ext {
        android_minSdkVersion     = 15
        android_targetSdkVersion  = 19
        android_compileSdkVersion = 19
        android_buildToolsVersion = "23.0.3"
    }

    group = "mil.army.missioncommand"

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencyManagement {
        dependencies {
            dependency ("com.android.support:support-v4:23.2.1")
            dependency ("com.android.support:support-annotations:23.2.1")
            dependency ("com.google.android.gms:play-services:6.5.87")
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }
}

def updatePom(pom, gradleProject) {
    pom.withXml { XmlProvider xml ->
        def root = xml.asNode()

        /*
         * inject dependencies
         */
        def dependencies = root.appendNode('dependencies')
        gradleProject.configurations.compile.allDependencies.each {
            def dependency = dependencies.appendNode('dependency')
            dependency.appendNode('groupId'   , it.group)
            dependency.appendNode('artifactId', it.name)
            if (it.version != null) {
                dependency.appendNode('version', it.version)
            }
        }

        /*
         * inject name/description
         */
        root.appendNode('name'       , gradleProject.name)
        root.appendNode('description', gradleProject.description)
    }
}

task wrapper(type: Wrapper, description: "Generates gradlew[.bat] scripts") {
    gradleVersion = '3.4'
}
